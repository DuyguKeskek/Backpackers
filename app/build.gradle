apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.getkeepsafe.dexcount'
apply from: '../config/quality/quality.gradle'

ext.minimumSdkVersion = 16
ext.versionMajor = 1
ext.versionMinor = 0
ext.versionPatch = 0
ext.versionClassifier = "RELEASE"

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'
    defaultConfig {
        applicationId "com.backpackers.android"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode generateVersionCode() // 140010203
        versionName generateVersionName() // 1.2.3-SNAPSHOT
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true;

        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        buildTypes.each {
            it.buildConfigField 'String', 'CLIENT_SERVER_ID', '"634217538679-slgv0gogb54vve15k3riccccf16dsp06.apps.googleusercontent.com"'
        }
    }

    /*splits {
        abi {
            enable true
            reset()
            include 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'mips', 'x86', 'x86_64'
            universalApk true
        }
    }*/

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        javaMaxHeapSize "5g"
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
}

private int generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 +
            ext.versionMajor * 10000 +
            ext.versionMinor * 100 +
            ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier != null && !ext.versionClassifier.isEmpty()) {
        versionName = versionName + "-" + ext.versionClassifier
    }
    return versionName;
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(path: ':backend', configuration: 'android-endpoints')

    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:support-v4:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:palette-v7:$SUPPORT_LIBRARY_VERSION"

    compile "com.google.firebase:firebase-messaging:$PLAY_SERVICES_VERSION"

    compile "com.google.android.gms:play-services-awareness:$PLAY_SERVICES_VERSION"
    compile "com.google.android.gms:play-services-analytics:$PLAY_SERVICES_VERSION"
    compile "com.google.android.gms:play-services-gcm:$PLAY_SERVICES_VERSION"
    compile "com.google.android.gms:play-services-auth:$PLAY_SERVICES_VERSION"

    compile "com.jakewharton:butterknife:$BUTTERKNIFE_VERSION"
    apt "com.jakewharton:butterknife-compiler:$BUTTERKNIFE_VERSION"

    compile 'com.android.support.constraint:constraint-layout:1.0.0-beta4'
    compile 'com.github.hotchemi:permissionsdispatcher:2.2.0'
    apt 'com.github.hotchemi:permissionsdispatcher-processor:2.1.3'

    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.9'

    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.4.0@aar'

    compile 'com.android.support:multidex:1.0.1'
    compile 'com.yalantis:ucrop:2.2.0-native'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.jakewharton.timber:timber:4.3.1'
    compile 'com.github.armcha:AutoLinkTextView:0.2.0'

    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
}

apply plugin: 'com.google.gms.google-services'
